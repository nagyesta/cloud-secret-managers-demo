plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'com.avast.gradle.docker-compose' version '0.16.13'
}

group = 'com.github.nagyesta.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'com.azure:azure-security-keyvault-secrets'
    implementation 'org.apache.commons:commons-dbcp2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    dependencies {
        dependency 'com.azure:azure-security-keyvault-secrets:4.6.4'
        dependency 'org.apache.commons:commons-dbcp2:2.9.0'
        dependency 'com.mysql:mysql-connector-j:8.1.0'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

dockerCompose {
    useComposeFiles = ['docker-compose.yml']
    forceRecreate = true
    waitForTcpPorts = true
    retainContainersOnStartupFailure = false
    captureContainersOutput = false

    projectName = 'akv-demo'
    dockerComposeWorkingDirectory = project.file("${projectDir}/local")
}

bootRun {
    systemProperty("javax.net.ssl.trustStore", file("${projectDir}/local/local-certs.p12"))
    systemProperty("javax.net.ssl.trustStorePassword", "changeit")
    systemProperty("spring.profiles.active", "dev")
    dependsOn tasks.composeUp
    finalizedBy tasks.composeDown
}
